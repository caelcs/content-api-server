apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'spring-boot'
apply plugin: 'pl.allegro.tech.build.axion-release'

sourceSets {
    //Declaring this source set creates compile time and runtime configurations for it.
    integrationTest {
        java {
            srcDir file('src/integrationTest/java')
        }
        resources {
            srcDir file('src/integrationTest/resources')
        }

        compileClasspath += main.output + test.output + configurations.integrationTestCompile
        runtimeClasspath += compileClasspath + configurations.integrationTestRuntime
    }
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("pl.allegro.tech.build:axion-release-plugin:${axionReleasePluginVersion}")
        classpath("se.transmode.gradle:gradle-docker:${dockerVersion}")
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'uk.co.caeldev'

mainClassName = 'uk.co.caeldev.content.api.Application'

repositories {
  jcenter()
  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots"
  }
}

configurations.all {
  // check for updates every build
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

scmVersion {
    tag {
        prefix = 'RELEASE'
    }
}

version = scmVersion.version

jar {
    baseName = rootProject.name
    version = version
}

docker {
    maintainer = 'Adolfo Custidiano <adolfoecs@gmail.com>'
    baseImage = 'java:7'
}

task buildDocker(type: Docker, dependsOn: build) {
    push = true
    applicationName = "${jar.baseName}"
    tagVersion = "${jar.version}"
    tag = "adolfoecs/${applicationName}"

    runCommand("mkdir -p /opt/applications")

    addFile("${buildDir}/distributions/${project.name}-${project.version}.tar", "/opt/applications/")

    workingDir("/opt/applications/${project.name}-${project.version}/")

    entryPoint(["bin/${project.name}"])

}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

    testCompile.exclude group: 'org.mortbay.jetty', module: 'servlet-api'
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-aop")
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    compile("org.springframework.cloud:spring-cloud-security:${springCloudSecurityVersion}")
    compile("org.springframework.hateoas:spring-hateoas")
    compile("com.google.guava:guava:${guavaVersion}")
    compile("org.springframework.security.oauth:spring-security-oauth2:${springSecurityOauth2Version}")
    compile("com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-joda:${jacksonDatabindVersion}")

    compile("joda-time:joda-time:${jodaTimeVersion}")
    compile("uk.co.caeldev:spring-security-mongo:${springSecurityMongoVersion}")
    compile("uk.co.caeldev:spring-mvc-utils:${springMvcUtilsVersion}")
    compile("org.apache.commons:commons-lang3:${apacheCommonsLang3Version}")

    compile("org.springframework.boot:spring-boot-starter-tomcat")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.assertj:assertj-core:${assertjCoreVersion}")
    testCompile("uk.org.fyodor:fyodor-core:${fyodorVersion}")
    testCompile("uk.org.fyodor:fyodor-junit:${fyodorVersion}")
    testCompile("uk.org.fyodor:fyodor-jodatime:${fyodorVersion}")
    testCompile("com.github.fakemongo:fongo:${fongoVersion}")
    testCompile("com.lordofthejars:nosqlunit-mongodb:${nosqlunitMongodbVersion}")
    testCompile("com.jayway.jsonpath:json-path:${jsonPathVersion}")
    testCompile("com.jayway.restassured:rest-assured:${restAssuredVersion}")
    testCompile("org.scribe:scribe:${scribeVersion}")
    testCompile("com.github.tomakehurst:wiremock:${wiremockVersion}")

    integrationTestCompile configurations.testCompile
    integrationTestRuntime configurations.testRuntime

}

task integrationTest(type: Test, group: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task allTests(dependsOn: ['test', 'integrationTest'])

check.dependsOn integrationTest

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task wrapper(type: Wrapper) {
    gradleVersion = rootGradleVersion
}
